grammar org.xtext.example.mydsl1.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl1/MyDsl"


BrowserDsl:
    cases+=TestCase*;

TestCase:
	'Test' 'case' ':' name=STRING '***' 
    commands+=Command*
	'***'
;
Command:
    OpenBrowser |
    GoTo |
    DOMCommand;
    
OpenBrowser:
    {OpenBrowser} 'open' 'a' 'browser' 'window';

DOMCommand:
    Click | Insert | Copy | Paste | Verify
;
GoTo:
    'go' 'to' url=STRING;

Click:
    {Click} (('click' 'on') | 'select') element=Selector;

Copy:
    {Copy} 'copy' 'the' 'property' property=HTML_ATTRIBUTE 'of' element=Selector 'as' key=STRING;

Paste:
    'paste' 'the' 'content' 'of' 'the' 'clipboard' 'in' element=Selector;

Insert:
    'fill' element=Selector 'with' content=Content;

Selector:
    predicate=SelectorPredicate 
    domType=STRING 
    ('with' 'properties' attributes=Attributes)?
    ('with' 'associated' 'label' label=STRING)?
    ('that' 'is' properties=Properties)?
    ('in' parent=Selector)?
;

Attributes:
	attributes+=ElementAttribute ('and' properties+=ElementAttribute)*
;

ElementAttribute:
	(name=HTML_ATTRIBUTE matcher=Matcher value=Content)
;

Content: ClipboardContent | StringContent;

ClipboardContent: 'the' 'saved' 'content' key=STRING;

StringContent: value=STRING;


Properties:
	properties+=ElementProperty ('and' properties+=ElementProperty)*
;
ElementProperty:
	negated=("not")? property=("selected" | "visible" | "enabled")
;
SelectorPredicate:
    AllPredicate | 
    OrdinalPredicate | 
    LastPredicate | 
    DefaultFirstPredicate
;

AllPredicate:
	{AllPredicate}'all' 'elements' 'of' 'type'
;

OrdinalPredicate:
	'the' ordinal=OrdinalInteger
;

LastPredicate:
	{LastPredicate}'the' 'last'
;

DefaultFirstPredicate:
	{DefaultFirstPredicate} 'the'
;

Matcher:
	"containing" | "matching"
;

Verify:
    'verify' 'that' selector=Selector 'is' properties+=ElementProperty ('and' properties+=ElementProperty)*;
    
terminal ORDINAL_SUFFIX:'st' | 'nd' | 'rd' | 'th';

OrdinalInteger:
  value=INT ORDINAL_SUFFIX;
  
terminal HTML_ATTRIBUTE: ('a'..'z'|'A'..'Z'|'_'|'-') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;